buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 7
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    google()
    jcenter()
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true" == System.getenv("TRAVIS")
def preDexEnabled = "true" == System.getProperty("pre-dex", "true")

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    signingConfigs {
        progscrape {
            storeFile file('progscrape.keystore')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
    }

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 28

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        signingConfig signingConfigs.progscrape

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
    }

    flavorDimensions "version"

    productFlavors {
        internal {
            applicationId 'com.progscrape.internal'
            dimension "version"
        }
        production {
            applicationId 'com.progscrape'
            dimension "version"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // https://code.google.com/p/android/issues/detail?id=61573
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

// TODO remove eventually: http://b.android.com/162285
configurations {
    internalDebugCompile
}

dependencies {
    implementation project(':data')
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.octo.android.robospice:robospice:1.4.14'
    implementation 'com.octo.android.robospice:robospice-okhttp:1.4.14'
    implementation 'com.grack:nanojson:1.7'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'com.squareup:otto:1.3.8'

    implementation 'com.google.dagger:dagger:2.37'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.37'

    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'net.danlew:android.joda:2.10.9.1'
    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'frankiesardo:icepick:3.2.0'
    annotationProcessor 'frankiesardo:icepick-processor:3.2.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation('org.robolectric:robolectric:4.5.1') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}
